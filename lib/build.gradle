plugins {
    // This is a core plugin and versioned together with Gradle
    id 'maven-publish'
    // This is a core plugin and versioned together with Gradle
    id 'signing'
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
}

def srcDirs = [
        'mobi/lab/components/compose/button',
        'mobi/lab/components/compose/color',
        'mobi/lab/components/compose/shape',
        'mobi/lab/components/compose/shared',
        'mobi/lab/components/compose/textfield',
        'mobi/lab/components/compose/theme',
        'mobi/lab/components/compose/toolbar',
        'mobi/lab/components/compose/typography',
]

project.archivesBaseName = "labcomponents-compose"

android {
    namespace "mobi.lab.components.compose"

    // A trick taken from material-components-android to keep everything in a single R namespace
    // and still keep the sources separate for maintainability.
    sourceSets {
        main.java.srcDir 'java'
        main.resources.srcDir 'java/resources'
        main.java.excludes = [
                '**/build/**',
        ]
        srcDirs.forEach {
            main.res.srcDirs += 'java/' + it + '/res'
            main.res.srcDirs += 'java/' + it + '/res-public'
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    kotlinOptions {
        jvmTarget = "1.8"

        // For Kotlin API strict mode.
        // In the future we should be able to do it easier.
        // When https://youtrack.jetbrains.com/issue/KT-37652 is fixed
        // then we can use the module annotation to enable this instead of the compiler flag.
        freeCompilerArgs += '-Xexplicit-api=strict'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        merge '**/LICENSE.txt'
    }

    defaultConfig {
        compileSdkVersion libs.versions.android.sdk.compile.get()
        targetSdkVersion libs.versions.android.sdk.target.get()
        minSdkVersion libs.versions.android.sdk.min.get()
        setVersion "${rootProject.ext.libraryVersion}"
    }

    buildTypes.all {
        consumerProguardFiles 'proguard-rules.pro'
    }

    testOptions.unitTests.all {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
            // Always show output even if the result has not changed
            outputs.upToDateWhen { false }
            // Show standard stream outputs if used
            showStandardStreams = true
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.getName().contains("release")) {
                // Don't add the variant name to release artefacts
                outputFileName = "${archivesBaseName}-${rootProject.ext.libraryVersion}.aar"
            } else {
                outputFileName = "${archivesBaseName}-${variant.name}-${rootProject.ext.libraryVersion}.aar"
            }
        }
    }

    lint {
        fatal "StopShip"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation libs.kotlin
    implementation libs.material
}


def GROUP_CI_BUILD_TASKS = "Build"

task buildLibDebug(dependsOn: ["build"])
buildLibDebug.group = GROUP_CI_BUILD_TASKS
buildLibDebug.description = "Build the debug version of components lib"

task buildLibRelease(dependsOn: ["buildLibDebug"])
buildLibRelease.group = GROUP_CI_BUILD_TASKS
buildLibRelease.description = "Build the release version of components lib"

task buildAndPublishRelease(dependsOn: ['buildLibRelease', 'publish'])
buildAndPublishRelease.group = GROUP_CI_BUILD_TASKS
buildAndPublishRelease.description = "Build the library release version and publish it"

publish.mustRunAfter tasks.findByPath('buildLibRelease')

Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('publish.properties').newDataInputStream())
} catch (Exception ignored) {
    println "publish.properties missing"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'mobi.lab.labcomponents'
                artifactId = 'labcomponents-compose'
                from components.release
                pom {
                    packaging = 'aar'
                    name = 'Mobi Lab Components for Compose library.'
                    description = 'The Compose implementation of the Mobi Lab design system components.'
                    url = 'https://github.com/LabMobi/labcomponents-compose'

                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            name = properties.getProperty("publish_developer_name")
                            name = properties.getProperty("publish_developer_name")
                            email = properties.getProperty("publish_developer_email")
                            organization = properties.getProperty("publish_developer_organization")
                            organizationUrl = properties.getProperty("publish_developer_organization_url")
                        }
                    }
                    scm {
                        connection = 'https://github.com/LabMobi/labcomponents-compose.git'
                        developerConnection = 'https://github.com/LabMobi/labcomponents-compose.git'
                        url = 'https://github.com/LabMobi/labcomponents-compose'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username = properties.getProperty("publish_ossrh_username")
                    password = properties.getProperty("publish_ossrh_password")
                }
            }
        }
    }

    signing {
        def signingKey = properties.getProperty("signing_key")
        def signingPassword = properties.getProperty("signing_password")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}
